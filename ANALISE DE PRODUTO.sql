DECLARE @FATOR_MES INT = 6,
		@FATOR_GIRO FLOAT = 1.5,
		@FORNECEDOR INT = 8745,
		@GRUPO INT,
		@PRODUTO INT,
		@EMPRESA INT,
		@DTCALC DATETIME,
		@DTINI DATETIME,
		@DTFIM DATETIME,
		@DTATUAL DATETIME,
		@TODOS VARCHAR(1) = 'N';

SET @DTCALC = DATEADD(MONTH, (@FATOR_MES * -1), GETDATE())
SET @DTINI = DATEADD(DAY, 1, EOMONTH(@DTCALC, -1))
SET @DTFIM  = DATEADD(DAY, 1, DATEADD(SECOND, -1, CAST(EOMONTH(GETDATE(), -1) AS DATETIME)))
SET @DTATUAL = DATEADD(DAY, 1, EOMONTH(GETDATE(), -1));

WITH VENDAS AS (
	SELECT
		p.CODPROD,
		v.CONTROLE,
		ven.MES,
		ven.QTDFAT,
		ROW_NUMBER() OVER (PARTITION BY p.CODPROD, v.CONTROLE ORDER BY ven.QTDFAT ASC)MENORVALOR,
		ROW_NUMBER() OVER (PARTITION BY p.CODPROD, v.CONTROLE ORDER BY ven.QTDFAT DESC)MAIORVALOR,
		COUNT(*) OVER (PARTITION BY p.CODPROD, v.CONTROLE) QTD_REPETI
	FROM
		TGFPRO p
		INNER JOIN TGFVOA v ON v.CODPROD = p.CODPROD AND p.CODVOL = v.CODVOL
		OUTER APPLY (
			SELECT
				i.CODPROD,
				i.CONTROLE,
				MONTH(c.DTFATUR)MES,
				SUM(i.QTDNEG * t.GOLDEV)QTDFAT
			FROM
				TGFCAB c
				INNER JOIN TGFTOP t ON t.CODTIPOPER = c.CODTIPOPER
				INNER JOIN TGFITE i ON i.NUNOTA = c.NUNOTA
			WHERE
				t.DHALTER = c.DHTIPOPER
				AND c.TIPMOV IN ('V', 'D')
				AND (t.GOLSINAL = -1 OR BONIFICACAO = 'S')
				AND i.CODPROD = p.CODPROD
				AND i.CONTROLE = v.CONTROLE
				AND c.DTFATUR BETWEEN @DTINI AND @DTFIM
				AND IIF(@EMPRESA IS NULL, 1, c.CODEMP) = IIF(@EMPRESA IS NULL, 1, @EMPRESA)
			GROUP BY
				i.CODPROD,
				i.CONTROLE,
				MONTH(c.DTFATUR)
		)ven
	WHERE
		p.ATIVO = 'S'
		AND IIF(@FORNECEDOR IS NULL, 1, p.CODPARCFORN) = IIF(@FORNECEDOR IS NULL, 1, @FORNECEDOR)
		AND IIF(@GRUPO IS NULL, 1, p.CODGRUPOPROD) = IIF(@GRUPO IS NULL, 1, @GRUPO)
		AND IIF(@PRODUTO IS NULL, 1, p.CODPROD) = IIF(@PRODUTO IS NULL, 1, @PRODUTO)
), PRODUTOS AS (
select
	*
from
	VENDAS
WHERE
	MAIORVALOR > IIF(QTD_REPETI > 2, 1, 0)
	AND MENORVALOR > IIF(QTD_REPETI > 2, 1, 0)
	AND MES IS NOT NULL
), LISTA_PROD AS (
	
SELECT
	p.CODPROD,
	p.CONTROLE,
	AVG(QTDFAT) MEDIA_VENDA_MES
FROM
	PRODUTOS p
GROUP BY
	p.CODPROD,
	p.CONTROLE
), CALCULOS AS (
SELECT
	l.*,
	ISNULL(e.ESTOQUE, 0)ESTOQUE
FROM
	LISTA_PROD l
	OUTER APPLY (
		SELECT
			SUM(e.ESTOQUE)ESTOQUE
		FROM
			TGFEST e
		WHERE
			e.CODPROD = l.CODPROD
			AND e.CONTROLE = l.CONTROLE
			AND e.CODLOCAL = 1
			AND IIF(@EMPRESA IS NULL, 1, e.CODEMP) = IIF(@EMPRESA IS NULL, 1, @EMPRESA)
	)e
), VALIDA_FALTA AS (
SELECT
	c.*,
	ISNULL(ROUND((ESTOQUE / NULLIF(MEDIA_VENDA_MES, 0)), 2),0 )GIRO,
	ISNULL(ROUND(((ESTOQUE / NULLIF(MEDIA_VENDA_MES, 0)) * DAY(EOMONTH(GETDATE()))), 0), 0)DIAS_DE_ESTOQUE,
	ROUND((MEDIA_VENDA_MES * @FATOR_GIRO), 0) ESTOQUE_IDEAL_MES,
	ROUND((MEDIA_VENDA_MES * @FATOR_GIRO) - ESTOQUE, 0) FALTA_ESTOQUE_IDEAL
FROM
	CALCULOS c
), RESULT AS (
SELECT
	 r.CODPROD,
	 r.CONTROLE,
	 p.DESCRPROD,
	 ROUND(r.ESTOQUE,0)ESTOQUE,
	 ROUND(r.MEDIA_VENDA_MES, 0)MEDIA_VENDA_MES,
	 r.GIRO,
	 r.DIAS_DE_ESTOQUE,
	 r.ESTOQUE_IDEAL_MES,
	 IIF(r.FALTA_ESTOQUE_IDEAL < 0, 0, r.FALTA_ESTOQUE_IDEAL)FALTA_ESTOQUE_IDEAL
FROM
	VALIDA_FALTA r
	INNER JOIN TGFPRO p ON p.CODPROD = r.CODPROD
), VENDAATUAL AS (
SELECT
	*
FROM
	RESULT
WHERE
	FALTA_ESTOQUE_IDEAL >= IIF(@TODOS = 'S', 0, 1)
), ESTOQUE AS (

SELECT 
	p.*,
	ISNULL(ven.QTDFAT, 0) QTD_MES_ATUAL
FROM 
	VENDAATUAL p
	OUTER APPLY (
			SELECT
				SUM(i.QTDNEG * t.GOLDEV)QTDFAT
			FROM
				TGFCAB c
				INNER JOIN TGFTOP t ON t.CODTIPOPER = c.CODTIPOPER
				INNER JOIN TGFITE i ON i.NUNOTA = c.NUNOTA
			WHERE
				t.DHALTER = c.DHTIPOPER
				AND c.TIPMOV IN ('V', 'D')
				AND (t.GOLSINAL = -1 OR BONIFICACAO = 'S')
				AND i.CODPROD = p.CODPROD
				AND i.CONTROLE = p.CONTROLE
				AND c.DTFATUR >= @DTATUAL
				AND IIF(@EMPRESA IS NULL, 1, c.CODEMP) = IIF(@EMPRESA IS NULL, 1, @EMPRESA)
		)ven

), RESULTADO AS (

SELECT 
	p.*,
	ISNULL(DATEDIFF(DAY, u.dt, GETDATE()), 0)DIAS_SEM_ESTOQUE,
	ROUND((MEDIA_VENDA_MES / 30.6), 2)MEDIA_VENDA_DIA,
	preco.VLRVENDA PRECO
FROM 
	ESTOQUE p
	OUTER APPLY (
		SELECT 
			MAX(CAST(Dtref AS DATE))dt 
		FROM 
			CND_ESTDIA e
		WHERE
			e.codprod = p.CODPROD
			AND p.ESTOQUE = 0
	)u
	OUTER APPLY (
		SELECT 
			MAX(e.NUTAB)MAXNUTAB
		FROM
			TGFTAB t
			INNER JOIN TGFEXC e ON e.NUTAB = t.NUTAB 
		WHERE 
			e.CODPROD = p.CODPROD
			AND t.CODTAB = 0
	)tab 
	OUTER APPLY (
		SELECT 
			MAX(e.VLRVENDA)VLRVENDA
		FROM
			TGFTAB t
			INNER JOIN TGFEXC e ON e.NUTAB = t.NUTAB 
		WHERE 
			e.CODPROD = p.CODPROD
			AND e.NUTAB = tab.MAXNUTAB
	)preco 
)

SELECT 
	*,
	((MEDIA_VENDA_DIA * DIAS_SEM_ESTOQUE) * PRECO) PERCA
FROM 
	RESULTADO
ORDER BY DIAS_SEM_ESTOQUE DESC




/*
	SELECT DATEADD(DAY, 1, EOMONTH(GETDATE(), -1)) AS PrimeiroDiaDoMesAtual;
	SELECT CAST(EOMONTH(GETDATE())AS DATETIME) AS UltimoDiaDoMesAnterior;
	SELECT DATEADD(SECOND, -1, DATEADD(MONTH, 1, CAST(EOMONTH(GETDATE()) AS DATETIME))) AS UltimoDiaComHora;
	SELECT DATEADD(SECOND, -1, DATEADD(MONTH, 1, CAST(EOMONTH(GETDATE()) AS DATETIME))) AS UltimoDiaComHora;


*/


